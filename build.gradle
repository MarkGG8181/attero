plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.1.0'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://raw.githubusercontent.com/WonderlandCentral/wonderland-repository/main" }
}

configurations {
    library
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "maven.modrinth:sodium:${project.sodium_version}"

    library 'com.jfposton.ytdlp:yt-dlp-java:1.0'

    // From https://projectlombok.org/setup/gradle
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'

    testCompileOnly 'org.projectlombok:lombok:1.18.38'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.38'

    // For clovr.store
    library 'clovr.store:grabber:1.1'
    library 'org.bouncycastle:bcprov-jdk18on:1.80'

    // From https://github.com/FlorianMichael/fabric-imgui-example-mod
    library "io.github.spair:imgui-java-binding:${project.imgui_version}"
    library "io.github.spair:imgui-java-lwjgl3:${project.imgui_version}"
    library "io.github.spair:imgui-java-natives-windows:${project.imgui_version}"
    library "io.github.spair:imgui-java-natives-linux:${project.imgui_version}"
    library "io.github.spair:imgui-java-natives-macos:${project.imgui_version}"

    configurations.implementation.extendsFrom(configurations.library)
}

loom {
    accessWidenerPath = file("src/main/resources/attero.accesswidener")
}

shadowJar {
    configurations = [project.configurations.library]
    archiveClassifier.set("shadow")
    mergeServiceFiles()
}

configurations.configureEach {
    resolutionStrategy.eachDependency {
        if (requested.group == "org.lwjgl") {
            useVersion "3.3.3"
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

javafx {
    version = "21"
    modules = [ 'javafx.controls', 'javafx.media', 'javafx.swing' ]
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

build {
    dependsOn shadowJar
}

remapJar {
    input.set(shadowJar.archiveFile)
}